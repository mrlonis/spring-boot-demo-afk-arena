plugins {
    id "java"
    id "idea"
    id "jacoco"
    id "org.springframework.boot" version "2.5.6"
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "io.freefair.lombok" version "6.2.0"
    id "org.flywaydb.flyway" version "6.5.7"
}

group = 'com.mrlonis.springbootdemoafkarena'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

jacoco {
    toolVersion = "0.8.7"
}

def filesToExcludeFromCoverage = [""]

task restartDocker {
    doLast {
        exec {
            //commandLine 'sh', './scripts/restartDocker.sh'
            workingDir = file('./scripts/')
            commandLine = ['cmd', '/C', 'restartDocker', 'restartDocker.bat']
        }
    }
}

test {
    finalizedBy jacocoTestReport
    dependsOn restartDocker
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }

    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it,
                     exclude: filesToExcludeFromCoverage)
        })
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it,
                     exclude: filesToExcludeFromCoverage)
        })
    }

    violationRules {
        rule {
            limit {
                minimum = 0.00
            }
        }

        rule {
            enabled = false
            element = 'CLASS'
            includes = ['org.gradle.*']

            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                maximum = 0.3
            }
        }
    }

    mustRunAfter jacocoTestReport
}

check {
    dependsOn jacocoTestCoverageVerification
}

bootRun {
    systemProperties = System.properties as Map<String, ?>
}

build {
    dependsOn jacocoTestReport
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.microsoft.sqlserver:mssql-jdbc:7.4.1.jre11'
    implementation 'com.zaxxer:HikariCP'
    implementation "io.springfox:springfox-boot-starter:3.0.0"
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-integration'
    implementation "org.springframework.boot:spring-boot-starter-web"
//    implementation 'org.springframework.cloud:spring-cloud-starter-bus-amqp'
    implementation 'org.springframework.integration:spring-integration-core'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.amqp:spring-rabbit-test'
}